#version 460
#extension GL_EXT_ray_tracing : enable

struct Payload
{
    vec4 color;
    bool isShadowed;
	int  depth;
};

layout(location = 0) rayPayloadEXT Payload payload;

layout(binding = 0, set = 0) uniform accelerationStructureEXT as;

layout(binding = 1, set = 0, rgba32f) uniform image2D img;

layout(binding = 2, set = 0) uniform UniformBuffer
{
    mat4 view_inv;
    mat4 proj_inv;
    float time;
} uniform_buffer;

vec3 linearToSrgb(vec3 color) 
{
    vec3 result;
    // Conversion formula from linear RGB to sRGB
    result.r = (color.r <= 0.0031308) ? (12.92 * color.r) : (1.055 * pow(color.r, 1.0 / 2.4) - 0.055);
    result.g = (color.g <= 0.0031308) ? (12.92 * color.g) : (1.055 * pow(color.g, 1.0 / 2.4) - 0.055);
    result.b = (color.b <= 0.0031308) ? (12.92 * color.b) : (1.055 * pow(color.b, 1.0 / 2.4) - 0.055);
    return result;
}

void main()
{
  vec2 pixelCenter = vec2(gl_LaunchIDEXT.xy) + vec2(0.5);
  vec2 uv = pixelCenter / vec2(gl_LaunchSizeEXT.xy);
  vec2 d = uv * 2.0 - 1.0;

  vec3 ro = vec3(uniform_buffer.view_inv * vec4(0, 0, 0, 1)).xyz;
  vec4 target = uniform_buffer.proj_inv * vec4(d.x, d.y, 0.1, 1);
  vec3 rd = vec3(uniform_buffer.view_inv * vec4(normalize(target.xyz), 0)).xyz;

  payload.color = vec4(0);
  payload.depth = 0;
  traceRayEXT(
    as,
    gl_RayFlagsOpaqueEXT,
    0xff,
    0, 0, 0,
    ro, 0.0001, rd, 10000.0,
    0
  );

  imageStore(img, ivec2(gl_LaunchIDEXT), vec4(linearToSrgb(payload.color.xyz), 1.0));
}
